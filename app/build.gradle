apply plugin: 'com.android.application'

def time = "${new Date().format("yyyyMMdd")}"//
def codename = "1"//当日迭代号
android {
    compileSdkVersion build_versions.target_sdk
    defaultConfig {
        applicationId "com.zqy.sui.demo"
        minSdkVersion build_versions.min_sdk
        targetSdkVersion build_versions.target_sdk
        multiDexEnabled true

        ndk {
            //选择要添加的对应cpu类型的.so库。
            abiFilters 'armeabi', 'armeabi-v7a'
            // 还可以添加 'x86', 'x86_64', 'mips', 'mips64'
        }
        dexOptions {
            //避免打包时内存不足，导致一个dex不完整，合并的时候发现大小不一致而报错
            javaMaxHeapSize "4g"
        }
    }

    //***********************************多渠道构建***************************************
    flavorDimensions "channel"
    productFlavors {
        SUI_Demo {
            dimension "channel"
            applicationId "com.zqy.sui.demo"
            versionCode 1
            versionName "1." + time + "." + codename
            manifestPlaceholders = [
                    APP_NAME      : "SUI-Demo",
                    APP_ICON      : "@mipmap/ic_launcher",
                    PACKAGE_NAME  : applicationId,
                    JSHARE_PKGNAME: applicationId,
                    JPUSH_PKGNAME : applicationId,
                    JPUSH_APPKEY  : "f43583a474c09d91cd138e01", //JPush上注册的包名对应的appkey.
                    JPUSH_CHANNEL : "gamemange000", //渠道号（不能使用中文）
                    TENCENT_APPID : "101551231",//腾讯开放平台注册应用得到的 appId,哪怕用不到也得放在这,不然manifest会有merge错误
            ]
        }
    }

    signingConfigs {
        config {
            storeFile file('../zqy_key.jks')
            storePassword "147258369"
            keyAlias "zqy"
            keyPassword "147258369"
        }

    }

    buildTypes {
        release {
            minifyEnabled false
            shrinkResources false
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
        }
        debug {
            minifyEnabled false
            shrinkResources false
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/rxjava.properties'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/MANIFEST.MF'
        doNotStrip '*/mips/*.so'
        doNotStrip '*/mips64/*.so'
    }
    //解决 Invoke-customs are only supported starting with Android O (--min-api 26)
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    /*Gradle3.3 以上的方法*/
    applicationVariants.all { variant ->
        // 更新至Android Studio 3.3 gradle 4.10.1
        variant.outputs.all {
            //重命名输出apk文件名
            outputFileName = "${getName()}(${new Date().format("yyyyMMddHHmm")}_${variant.versionName}_${variant.versionCode}).apk"
        }
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation 'androidx.multidex:multidex:2.0.1'
//    implementation project(path: ':s_request')
//    implementation project(path: ':s_utils')
    implementation project(path: ':s_ui')


}